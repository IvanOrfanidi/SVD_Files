execConfigureTraceETM()
{
  __var value;

  value = __readMemory32(0x40021028, "Memory");
  value &= ~0x00200000;                        // bit21 release GPIOE reset
  __writeMemory32(value, 0x40021028, "Memory");

  value = __readMemory32(0x40021014, "Memory");
  value |= 0x00200000;                        // bit21 Enable GPIOE  clock
  __writeMemory32(value, 0x40021014, "Memory");
  
  value = __readMemory32(0xE0042004, "Memory");
  // STM32 specific register Debug MCU configuration: => Trace_IOEN
  // DBG_MCU_CR : assign SYNC TRACE I/Os for
  // this outputs a sync packet of FF_FF_FF_7F
  // TRACE_IOEN = 1, DBG_SLEEP = 0, DBG_STOP = 0, DBG_STANDBY = 0 ???
  value &= ~0xE0;
  
  if ( __getTracePortSize() == 1 )
  {
    value |= 0x60; // bit 6,5  TRACE_MODE=01 TRACE_IOEN=1
    __writeMemory32(value, 0xE0042004, "Memory");
    
    // Configure GPIOE  2,3 as AF
    value = __readMemory32(0x48001000, "Memory");
    value &= ~0x000000F0;
    value |= 0x000000A0;                        
    __writeMemory32(value, 0x48001000, "Memory");
     
    // Configure GPIOE  2,3 output type push-pull
    value = __readMemory32(0x48001004, "Memory");
    value &= ~0x0000000C;
    __writeMemory32(value, 0x48001004, "Memory");

    // Configure GPIOE  2,3 No pull-up, pull-down
    value = __readMemory32(0x4800100C, "Memory");
    value &= ~0x000000F0;
    __writeMemory32(value, 0x4800100C, "Memory");
     
    // Configure GPIOE 2,3 as High Speed
    value = __readMemory32(0x48001008, "Memory");
    value |= 0x000000F0;
    __writeMemory32(value, 0x48001008, "Memory");

    // Enable mode AF0 for GPIOE 2,3
    value = __readMemory32(0x48001020, "Memory");
    value &= ~0x0000FF00;
    __writeMemory32(value, 0x48001020, "Memory");
  } 
  else
  if ( __getTracePortSize() == 2 )
  {
    value |= 0xA0; // bit 7,6,5  TRACE_MODE=10 TRACE_IOEN=1
    __writeMemory32(value, 0xE0042004, "Memory");
    
    // Configure GPIOE  2,3,4 as AF
    value = __readMemory32(0x48001000, "Memory");
    value &= ~0x000003F0;
    value |= 0x000002A0;                        
    __writeMemory32(value, 0x48001000, "Memory");
     
    // Configure GPIOE  2,3,4 output type push-pull
    value = __readMemory32(0x48001004, "Memory");
    value &= ~0x0000001C;
    __writeMemory32(value, 0x48001004, "Memory");

    // Configure GPIOE  2,3,4 No pull-up, pull-down
    value = __readMemory32(0x4800100C, "Memory");
    value &= ~0x000003F0;
    __writeMemory32(value, 0x4800100C, "Memory");
     
    // Configure GPIOE 2,3,4 as High Speed
    value = __readMemory32(0x48001008, "Memory");
    value |= 0x000003F0;
    __writeMemory32(value, 0x48001008, "Memory");

    // Enable mode AF0 for GPIOE 2,3,4
    value = __readMemory32(0x48001020, "Memory");
    value &= ~0x000FFF00;
    __writeMemory32(value, 0x48001020, "Memory");
  } 
  else   // default 4 bits
  {
    value |= 0xE0; // bit 7,6,5  TRACE_MODE=11 TRACE_IOEN=1
    __writeMemory32(value, 0xE0042004, "Memory");
    
    // Configure GPIOE  2,3,4,5,6 as AF
    value = __readMemory32(0x48001000, "Memory");
    value &= ~0x00003FF0;
    value |= 0x00002AA0;                        
    __writeMemory32(value, 0x48001000, "Memory");
     
    // Configure GPIOE  2,3,4,5,6 output type push-pull
    value = __readMemory32(0x48001004, "Memory");
    value &= ~0x0000007C;
    __writeMemory32(value, 0x48001004, "Memory");

    // Configure GPIOE  2,3,4,5,6  No pull-up, pull-down
    value = __readMemory32(0x4800100C, "Memory");
    value &= ~0x00003FF0;
    __writeMemory32(value, 0x4800100C, "Memory");
     
    // Configure GPIOE 2,3,4,5,6 as High Speed
    value = __readMemory32(0x48001008, "Memory");
    value |= 0x00003FF0;
    __writeMemory32(value, 0x48001008, "Memory");

    // Enable mode AF0 for GPIOE 2,3,4,5,6
    value = __readMemory32(0x48001020, "Memory");
    value &= ~0x0FFFFF00;
    __writeMemory32(value, 0x48001020, "Memory");
  }
}

execConfigureTraceSWO()
{
  __var value;
  
  value = __readMemory32(0x40021028, "Memory");
  value &= ~0x00040000;                        // bit18 release GPIOB reset
  __writeMemory32(value, 0x40021028, "Memory");

  value = __readMemory32(0x40021014, "Memory");
  value |= 0x00040000;                        // bit18 Enable GPIOB  clock
  __writeMemory32(value, 0x40021014, "Memory");

  value = __readMemory32(0xE0042004, "Memory");
  value &= ~0xE0;
  value |= 0x20;  // bit 7,6,5  TRACE_MODE=00 TRACE_IOEN=1
  __writeMemory32(value, 0xE0042004, "Memory");
  
  // Configure GPIOB  3 as AF
  value = __readMemory32(0x48000400, "Memory");
  value &= ~0x000000C0;
  value |= 0x00000080;                        
  __writeMemory32(value, 0x48000400, "Memory");
  
  // Configure GPIOB 3 output type push-pull
  value = __readMemory32(0x48000404, "Memory");
  value &= ~0x00000008;
  __writeMemory32(value, 0x48000404, "Memory");
  
  // Configure GPIOB 3 No pull-up, pull-down
  value = __readMemory32(0x4800040C, "Memory");
  value &= ~0x000000C0;
  __writeMemory32(value, 0x4800040C, "Memory");
  
  // Configure GPIOB 3 as High Speed
  value = __readMemory32(0x48000408, "Memory");
  value |= 0x000000C0;
  __writeMemory32(value, 0x48000408, "Memory");
  
  // Enable mode AF0 for GPIOB 3
  value = __readMemory32(0x48000420, "Memory");
  value &= ~0x0000F000;
  __writeMemory32(value, 0x48000420, "Memory"); 
}

_ExecDeviceExecutionStarted()
{
  if (__driverType("ijet") || __driverType("cmsisdap"))
  {
    // Allow debugging in power saving modes.
    
    __var dbgmcu_cr;

    dbgmcu_cr = __readMemory32(0xE0042004, "Memory");

    if ((dbgmcu_cr & 0x7) != 0x7) {
      
      dbgmcu_cr |= 7; // set DBGMCU_CR.DBG_SLEEP, DBG_STOP and DBG_STANDBY.
      
      __writeMemory32(dbgmcu_cr, 0xE0042004, "Memory");
      __message "DBGMCU_CR set to inhibit sleep-mode while debugging";
    }
  }
}


unlockFlash()
{
  // unlock Flash program and erase controller

  if(0x80 & __readMemory32(0x40022010,"Memory"))
  {
    __message "Unlocking FLASH";
    
    __writeMemory32(0x45670123, 0x40022004, "Memory"); // FLASH->FKEYR = FLASH_KEY1;
    __writeMemory32(0xCDEF89AB, 0x40022004, "Memory"); // FLASH->FKEYR = FLASH_KEY2;
    __writeMemory32(0xFFFFFFF8 & __readMemory32(0x40022000,"Memory"), 0x40022000, "Memory"); // Flash 0 wait state

  }
  else
  {
     __message "FLASH is already unlocked";
  }

}


lockFlash()
{
  // lock Flash program and erase controller
  
  if(!(0x80 & __readMemory32(0x40022010,"Memory")))
  {
    __message "Locking FLASH";
    
    __var temp_cr;
    temp_cr = __readMemory32(0x40022010,"Memory");
    
    __writeMemory32(temp_cr | 0x80, 0x40022010, "Memory"); // Set lock bit;
  
  }
  else
  {
    __message "FLASH is already locked";
  }

}

setOptionBytesWriteEnable()
{
  //Write enable for option bytes area

  if(!(0x200 & __readMemory32(0x40022010,"Memory")))
  {
  
    unlockFlash();
  
    __writeMemory32(0x45670123, 0x40022008, "Memory"); // FLASH->OPTKEYR = FLASH_OPTKEY1;
    __writeMemory32(0xCDEF89AB, 0x40022008, "Memory"); // FLASH->OPTKEYR = FLASH_OPTKEY2; 
    
    __message "Option bytes write enabled";
  
  }

}


readProtectionEnable()
{
// NOTICE
// This function will set read protection level 1, which can be disabled.
// Protection level 2 is irreversible and makes the JTAG interface permanently inoperable.
// See "Reference manual STM32F302xx, STM32F303xx and STM32F313xx advanced ARM-based 32-bit MCUs" for details.


  if ((0x6 & __readMemory32(0x4002201C,"Memory")) == 0)
  {
  
    setOptionBytesWriteEnable();
    
    __message "Setting FLASH readout protection level 1";
    
    __writeMemory32(0x00000220, 0x40022010, "Memory"); // Option byte erase
    __writeMemory32(0x00000260, 0x40022010, "Memory"); // Start operation
    
    while(0x1 & __readMemory32(0x4002200C,"Memory"));  // Wait while FLASH busy
    
    __writeMemory32(0x00000210, 0x40022010, "Memory"); // Option byte programming
    __writeMemory16(0x0000, 0x1FFFF800, "Memory");     // Enable readout protection
    
    while(0x1 & __readMemory32(0x4002200C,"Memory"));  // Wait while FLASH busy
    
    __hwReset(0); // Reset to make changes take effect
  
  }
  else
  {
    __message "Readout protection already set";
  }
  
}


readProtectionDisable()
{

  if ((0x6 & __readMemory32(0x4002201C,"Memory")) != 0)
  {

    __message "Reset FLASH readout protection";
    
    __writeMemory32(0x00000220, 0x40022010, "Memory"); // Option byte erase
    __writeMemory32(0x00000260, 0x40022010, "Memory"); // Start operation
    
    while(0x1 & __readMemory32(0x4002200C,"Memory"));  // Wait while FLASH busy
    
    __writeMemory32(0x00000210, 0x40022010, "Memory"); // Option byte programming
    __writeMemory16(0x55AA, 0x1FFFF800, "Memory");
    
    while(0x1 & __readMemory32(0x4002200C,"Memory"))
    {
    
      __delay(100);
    
    }  // Wait while FLASH busy
    
    __hwReset(0); // Reset to make changes take effect
  
  }
  else
  {
    __message "Readout protection already reset";
  }
  
}


getPersistentQuickLaunchMacros()   // <-- Reserved name, debugger calls it to populate QuickLaunch view.
{
  __message "getPersistentQuickLaunchMacros";
  __var macs;
  
  macs = "unlockFlash()";
  macs += ";lockFlash()";
  macs += ";readProtectionDisable()";
  macs += ";readProtectionEnable()";
  macs += ";setOptionBytesWriteEnable()";
  
  return macs;
}
